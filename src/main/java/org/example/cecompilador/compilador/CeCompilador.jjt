options
{
  static = true;
}

PARSER_BEGIN(CeCompilador)
package compilador;

import recovery.*;

public class CeCompilador
{
  public static void main(String args []) throws ParseException
  {
    CeCompilador parser = new CeCompilador(System.in);
    while (true)
    {
      System.out.println("Lendo da entrada padrao...");
      System.out.print("Insira uma expressao como \"mein {  inti x ; }\" :");
      try
      {
        //parser.main();
        SimpleNode n = CeCompilador.main();
        n.dump(" ");
      }
      catch (Exception e)
      {
        System.out.println("Ih rapaz...");
        System.out.println(e.getMessage());
        parser.ReInit(System.in);
      }
      catch (Error e)
      {
        System.out.println("Deu ruim.");
        System.out.println(e.getMessage());
        break;
      }
    }
  }

static public String im(int x)
  {
    int k;
	String s;
    s = tokenImage[x];
    k = s.lastIndexOf("\"");
    try {
      s = s.substring(1,k);
    }
   catch (StringIndexOutOfBoundsException e)
	   {}
   return s;
  }

  static Token lastError = null;
  static boolean eof;    // variável que indica se EOF foi alcançado

  // o método abaixo consome tokens até alcançar um que pertença ao conjunto
  // de sincronização
  static void consumeUntil(RecoverySet g,
	                 ParseException e,
	                 String met) throws ParseEOFException,
	                                    ParseException
  {
	Token tok;
	System.out.println();
	System.out.println("* " + met + " *");
	System.out.println("     Conjunto de sincronização: " + g);

	if (g == null) throw e; // se o conjunto é null, propaga a exceção

	tok = getToken(1); // pega token corrente
	while ( ! eof )	{ // se não chegou ao fim do arquivo
	  if ( g.contains(tok.kind)) {//achou um token no conjunto
	    System.out.println("     Encontrado token de sincronização: " +
	                       im(tok.kind));
	    break;
	  }
	  System.out.println("     Ignorando o token: " + im(tok.kind));
	  getNextToken();     // pega próximo token
      tok = getToken(1);
	  if (tok.kind == EOF && ! g.contains(EOF) ) // fim da entrada?
	      eof = true;
	}
    if ( tok != lastError)  {
	  System.out.println(e.getMessage());
	  lastError = tok;

	}
	if ( eof )
	  throw new ParseEOFException("Encontrei EOF onde não deveria.");
	}

}

PARSER_END(CeCompilador)

SKIP :
{
  " "
| "\r"
| "\t"
| "\n"
}

TOKEN : /* OPERATORS */
{
  < INICIAPROG : "mein" >
| < ABREBLOCO : "{" >
| < FECHABLOCO : "}" >
| < FIMINSTRUCAO: ";" >
| < TIPOINT : "inti" >
| < TIPOFLOAT : "flouti" >
| < TIPOBOL : "buleam" >
}

TOKEN : /* CONDITIONALS */
{
  < IF : "ifi" >
| < ABREPAR : "(" >
| < FECHAPAR : ")" >
| < OPREL : "==" | "!=" | "<" | ">" | "<=" | ">=" >
| < OPBOOL : "&&" | "||" >
}

TOKEN : /* ASSIGNMENT */
{
  < ASSIGN : "=" >
| < COMMA : "," >
}

TOKEN : /* LOOP */
{
  < WHILE : "uaio" >
| < DO : "du" >
| < DOWHILE : "uaiu" >
}

TOKEN : /* ARITHMETIC OPERATORS */
{
  < OPAD : "+" | "-" >
| < OPMULT : "*" | "/" >
}

TOKEN :
{
  < CONST_INT : (["0"-"9"])+ >
| < CONST_FLOAT : (["0"-"9"])+ "." (["0"-"9"])+ >
| < #DIGITO : [ "0"-"9" ] >
| < ID : ["a"-"z"](["a"-"z"] | < DIGITO >)* >
}

SimpleNode main() :
{RecoverySet follow = First.main;}
{

	try {
		< INICIAPROG > < ABREBLOCO > (instrucao())* < FECHABLOCO >
 	} catch(ParseException e) {
		consumeUntil(follow, e, "main");
 	}

 	{
 	  return jjtThis;
 	}

}

void instrucao() :
{RecoverySet follow = First.instrucao;}
{
    try {
        (declaracaoVariavel() | atribuicao() | expressao() < FIMINSTRUCAO >) | blocoInstrucoes() | estruturaCondicional() | estruturaRepeticao()
    } catch(ParseException e) {
        consumeUntil(follow, e, "instrucao");
    }
}

void blocoInstrucoes() :
{RecoverySet follow = First.blocoInstrucao;}
{
    try {
        < ABREBLOCO > (instrucao())* < FECHABLOCO >
    } catch(ParseException e) {
        consumeUntil(follow, e, "bloco de instrucoes");
    }
}

void declaracaoVariavel() :
{RecoverySet follow = First.declaraVariavel;}
{
	try {
		declaraTipo() < ID > (< COMMA > < ID >)* ( < ASSIGN > expressao() )? < FIMINSTRUCAO >
 	} catch(ParseException e) {
		consumeUntil(follow, e, "declaracao de variavel");
 	}

}

void declaraTipo() :
{}
{
    ( < TIPOINT > | < TIPOFLOAT > | < TIPOBOL > )
}

void atribuicao() :
{RecoverySet follow = First.atribuicao;}
{
    try {
        < ID > < ASSIGN > expressao() < FIMINSTRUCAO >
    } catch(ParseException e) {
        consumeUntil(follow, e, "atribuicao");
    }
}

void estruturaCondicional() :
{RecoverySet follow = First.estruturaCondicional;}
{
    try {
        < IF > < ABREPAR > expressao() < FECHAPAR > blocoInstrucoes()
    } catch(ParseException e) {
        consumeUntil(follow, e, "estrutura condicional");
    }
}

void estruturaRepeticao() :
{RecoverySet follow = First.estruturaRepeticao;}
{
    try {
        (estruturaWhile() | estruturaDoWhile())
    } catch(ParseException e) {
        consumeUntil(follow, e, "estrutura de repeticao");
    }
}


void estruturaWhile() :
{RecoverySet follow = First.estruturaWhile;}
{
    try {
        < WHILE > < ABREPAR > expressao() < FECHAPAR > blocoInstrucoes()
    } catch(ParseException e) {
        consumeUntil(follow, e, "estrutura while");
    }
}

void estruturaDoWhile() :
{RecoverySet follow = First.estruturaDoWhile;}
{
    try {
        < DO > blocoInstrucoes() < DOWHILE > < ABREPAR > expressao() < FECHAPAR > < FIMINSTRUCAO >
    } catch(ParseException e) {
        consumeUntil(follow, e, "estrutura do-while");
    }
}

void expressao() :
{RecoverySet follow = First.expressao;}
{
    try {
        expressaoRelacional() ( < OPBOOL > expressaoRelacional() )*
    } catch(ParseException e) {
        consumeUntil(follow, e, "expressao");
    }
}

void expressaoRelacional() :
{RecoverySet follow = First.expressaoRelacional;}
{
    try {
        expressaoAritmetica() ( < OPREL > expressaoAritmetica() )?
    } catch(ParseException e) {
        consumeUntil(follow, e, "expressao relacional");
    }
}


void expressaoAritmetica() :
{RecoverySet follow = First.expressaoAritmetica;}
{
    try {
        termo() ( < OPAD > termo() )*
    } catch(ParseException e) {
        consumeUntil(follow, e, "expressao aritmetica");
    }
}

void termo() :
{RecoverySet follow = First.termo;}
{
    try {
        fator() ( < OPMULT > fator() )*
    } catch(ParseException e) {
        consumeUntil(follow, e, "termo");
    }
}

void fator() :
{RecoverySet follow = First.fator;}
{
    try {
        < ID > | < CONST_INT > | < CONST_FLOAT > | < ABREPAR > expressao() < FECHAPAR >
    } catch(ParseException e) {
        consumeUntil(follow, e, "fator");
    }
}
